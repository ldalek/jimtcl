source [file dirname [info script]]/testing.tcl

needs cmd leval

test leval-1.1 {no args} -body {
    leval
} -returnCodes error -result {wrong # args: should be "leval string"}

test leval-1.2 {too many args} -body {
    leval
} -returnCodes error -result {wrong # args: should be "leval string"}

test leval-1.3 {basic, no subst} -body {
    leval {a b c}
} -result {a b c}

test leval-1.4 {basics, vars} -body {
    set a 1
    set b "2 3"
    set c "4 5 6"
    set d ".1"
    leval {$a $b $c$d}
} -result {1 {2 3} {4 5 6.1}}

test leval-1.5 {comments} -body {
    # It is helpful to be able to include comments in a list definition
    # just like in a script
    leval {
        # comment line
        1
        2 3
        # comment line with continuation \
        this is also a comments
        4 ;# comment at end of line
        5
    }
} -result {1 2 3 4 5}

test leval-1.6 {commands} -body {
    set a 0
    leval {
        [incr a]
        [incr a]
        [list d e]
        [string cat f g][string cat h i]
    }
} -result {1 2 {d e} fghi}

test leval-1.7 {expand} -body {
    set a {1 2}
	set space " "
    set b {3 4 5}
    leval {
        {*}$a
        {*}$a$space$b$space[list 6 7]
    }
} -result {1 2 1 2 3 4 5 6 7}

test leval-1.8 {empty case} -body {
	leval {
		# Nothing
	}
} -result {}

test leval-1.9 {backslash escapes} -body {
	leval {
		# char escapes
		\r\n\t
		# unicode escapes
		\u00b5
		# hex escapes
		\x41\x42
	}
} -result [list \r\n\t \u00b5 AB]

test leval-2.1 {error, missing [} -body {
	leval {
		# Missing bracket
		[string cat
	}
} -returnCodes error -result {unmatched "["}

test leval-2.2 {error, invalid command} -body {
	leval {
		a
		[dummy]
		b
	}
} -returnCodes error -result {invalid command name "dummy"}

test leval-2.3 {error, unset variable} -body {
	leval {
		a
		$doesnotexist
		b
	}
} -returnCodes error -result {can't read "doesnotexist": no such variable}

test leval-2.4 {break} -body {
	leval {
		a
		[break]
		b
	}
} -returnCodes error -result {invoked "break" outside of a loop}

test leval-2.5 {continue} -body {
	leval {
		a
		[continue]
		b
	}
} -returnCodes error -result {invoked "continue" outside of a loop}

test leval-3.1 {preservation of line numbers} -body {
	set x abc
	set src1 [info source $x]
	set list [leval {
		a
		$x
		b
	}]
	if {[info source [lindex $list 1]] ne [info source $x]} {
		error "source does not match
	}
} -result {}

testreport
